// properties文件配置
// 前缀 temp 临时变量
// 前缀 build 编译期设置参数，存入ext变量
// 前缀 bc buildConfigField
// 前缀 rv resValue
ext.initConfiguration = { projectTarget ->
    println("=> BOOT v1.0.0(build 2023/02/03 12:20:11)")
    println("=> BOOT BUILD START")
    def parseParam = { rawKey, text, localMap ->
        if (text == null || text.isEmpty()) {
            return ""
        }
        String openToken = "\${"
        String closeToken = "}"
        char[] src = text.toCharArray()
        int offset = 0;
        int start = text.indexOf(openToken, offset)
        if (start == -1) {
            return text
        }
        final StringBuilder builder = new StringBuilder();
        while(start > -1) {
            builder.append(src, offset, start - offset)
            offset = start + openToken.length()
            int end = text.indexOf(closeToken, offset)
            if (end == -1) {
                throw new IllegalAccessException("format error(NOT FOUND '}'), please watch key:" + rawKey)
            }

            String key = text.substring(offset, end)
            String value = localMap.get(key)
            builder.append(value)
            offset = end + closeToken.length()
            start = text.indexOf(openToken, offset)
        }
        if (offset < src.length) {
            builder.append(src, offset, src.length - offset);
        }

        return builder.toString()
    }
    Properties properties = new Properties()
    properties.load(new FileInputStream("${projectTarget.rootDir}/config/application.properties"))

    def localMap = new HashMap()
    properties.forEach() { key, value ->
        localMap.put(key, value)
    }

    if (ext.has("BUILD_TARGET")) {
        ext.BUILD_TARGET = ext.BUILD_TARGET.toLowerCase()
    } else {
        String buildTarget = localMap.get("BUILD_TARGET")
        ext.BUILD_TARGET = buildTarget
    }
    try {
        properties = new Properties()
        properties.load(new FileInputStream("${projectTarget.rootDir}/config/${ext.BUILD_TARGET}.properties"))
        properties.forEach() { key, value ->
            localMap.put(key, value)
        }
    } catch (Throwable e) {
    }

    // 去除环境变量，检查配置
    def paramMap = new HashMap()
    localMap.forEach() { key, value ->
        def start = key.indexOf(".", 0)
        if (start < 0) {
            if (paramMap.containsKey(key)) {
                throw new IllegalArgumentException("参数重复:" + key)
            }
            paramMap.put(key, value)
        } else {
            def k = key.substring(start + 1)
            if (paramMap.containsKey(k)) {
                throw new IllegalArgumentException("参数重复:" + k)
            }
            paramMap.put(k, value)
        }
    }

    // fill parameters
    ext.bootBCMap = new HashMap()
    ext.bootRVMap = new HashMap()
    localMap.forEach() { key, value ->
        def newValue = parseParam(key, value, paramMap)
        if (key.startsWith("build.")) {
            def k = key.substring(6)
            ext.setProperty(k, newValue)
//            println("build:put::$k = [$newValue]")
        } else if (key.startsWith("bc.")) {
            def k = key.substring(3)
            ext.bootBCMap.put(k, newValue)
            ext.setProperty(k, newValue)
//            println("bc:put::$k = [$newValue]")
        } else if (key.startsWith("rv.")) {
            def k = key.substring(3)
            ext.bootRVMap.put(k, newValue)
            ext.setProperty(k, newValue)
//            println("rv:put::$k = [$newValue]")
        }
    }
    println("=> BOOT BUILD END")
}

// 获取编译当前安装包的时间
def buildAppTime = {
    return new Date().format("yyyy_MM_dd_HH_mm")
}

// initBuildConfigFieldAndResValueScript 初始化生成BuildConfig和resValue变量脚本
ext.initBuildConfigFieldAndResValueScript = { param, sourceSets ->
    println("===============> GENERATE START <===============")
    def buildConfigFieldAndLog = { dc, type, key, value ->
        println("=> BOOT info: BuildConfig::$key = [$value]")
        if (type == "String") {
            dc.buildConfigField("String", key, "\"" + value + "\"")
        } else if (type == "long") {
            param.buildConfigField "long", key, "${value}L"
        }
    }
    def resValueAndLog = { dc, key, value ->
        println("=> BOOT info: resValue::$key = [$value]")
        dc.resValue("string", key, "${value}")
    }
    param.applicationId = BUILD_PACKAGE_NAME
    param.minSdkVersion = Integer.parseInt(BUILD_MIN_SDK_VERSION)
    param.targetSdkVersion = Integer.parseInt(BUILD_TARGET_API)
    param.versionCode = Integer.parseInt(BUILD_VERSION_CODE)
    param.versionName = BUILD_VERSION_NAME

    param.manifestPlaceholders.put("h5port", "um.${UMENG_KEY}")
    param.manifestPlaceholders.put("packagename", BUILD_PACKAGE_NAME)
    param.manifestPlaceholders.put("fileprovider", "${BUILD_PACKAGE_NAME}.fileprovider")
    param.manifestPlaceholders.put("channel_id", (CHANNEL_NAME.length() > 0) ? CHANNEL_NAME:"official")

    buildConfigFieldAndLog(param, "String", "BUILD_TIME", buildAppTime())
    buildConfigFieldAndLog(param, "long", "REQUEST_PERMISSION_START_TS", System.currentTimeMillis() + Integer.parseInt(REQUEST_PERMISSION_DELAY_HOURS) * 3600 * 1000L)
    bootBCMap.forEach() { key, value ->
        buildConfigFieldAndLog(param, "String", key, value)
    }

    resValueAndLog(param, "app_name", "${BUILD_APPLICATION_NAME}")
    resValueAndLog(param, "access_name", "★ ${BUILD_APPLICATION_NAME}")
    bootRVMap.forEach() { key, value ->
        resValueAndLog(param, key, value)
    }
    println("===============> GENERATE END <===============")

    println("info: sourceSets java/res/assets: src/${BUILD_TARGET.toLowerCase()}/[java/res/assets]")
    sourceSets.main.java.srcDirs += "src/${BUILD_TARGET.toLowerCase()}/java"
    sourceSets.main.res.srcDirs += "src/${BUILD_TARGET.toLowerCase()}/res"
    sourceSets.main.assets.srcDirs += "src/${BUILD_TARGET.toLowerCase()}/assets"

    android {
        defaultConfig{
            splits{
                abi {
                    def isDebugBuild = false
                    gradle.startParameter.taskNames.find {
                        if (it.toLowerCase().contains("debug")) {
                            isDebugBuild = true
                        }
                    }
                    def buildAllAbi = Boolean.parseBoolean(BUILD_ALL_ABI)
                    if (!isDebugBuild && !buildAllAbi) {
                        enable true
                        reset()
                        include "armeabi-v7a", "arm64-v8a"
                        universalApk false
                    } else {
                        // AS直接run到设备上
                        ndk {
                            abiFilters "armeabi-v7a", 'arm64-v8a'
                        }
                    }
                }
            }
        }
    }
}

